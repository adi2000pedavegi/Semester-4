EE18BTECH11034_1.NET - OPAMP CLOSED-LOOP STEP RESPONSE FOR PHASE MARGIN 51.9 DEGREES
* STEP INPUT
VS	1	0	AC	1	PWL(0US 0V   0.1US 1V   10US 1V)
*

R1	0	2	10
R2	2	4	990
XOP	1 2 	4	OPAMP1	


.SUBCKT OPAMP1      1   2 6
* INPUT IMPEDANCE
RIN	1	2	100MEG
*SETTING THE FIRST POLE(10 HZ) AND OPEN LOOP GAIN
G1      0       3  VALUE = { 1e-1*V(1,2)}
RP1     3      0   1e6
CP1     3      0   1.59e-8
*SETTING THE SECOND POLE(10K HZ)
G2      0       4  3 0 1e-6
RP2     4      0   1e6
CP2     4      0   1.59e-11
* OUTPUT BUFFER AND RESISTANCE
EBUFFER	5 0	4 0	1
ROUT	5	6	10
.ENDS

* ANALYSIS
.AC 	DEC 	10 10 10MEG
.TRAN 	0.01US  1MS
*

* VIEW RESULTS
.control
run
plot V(1)V(4)
wrdata ee18btech11034_1.dat V(4)
.endc 

.end

